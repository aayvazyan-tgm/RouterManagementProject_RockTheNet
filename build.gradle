apply plugin: 'java'
apply plugin: 'application'

/* IDEs */
apply plugin: 'eclipse'
apply plugin: 'idea'

/* Test coverage */
apply plugin: "jacoco"

sourceCompatibility = 1.8

ext.revision = 'git rev-list --count HEAD'.execute().text.trim()
ext.hash = 'git rev-parse --short HEAD'.execute().text.trim()
version = "r${revision}.${hash}"

repositories {
    mavenCentral()

    maven {
    	url "https://oosnmp.net/dist/release/" // snmp4j
    }
    maven {
        url "http://ci.truh.in/nexus/content/repositories/releases/"
    }
}

mainClassName = 'rtn.MainApp'

dependencies {
    compile 'com.jcraft:jsch:0.1.51'
    compile 'com.google.code.gson:gson:2.3'
    compile 'org.slf4j:slf4j-log4j12:1.7.7'
    compile 'org.controlsfx:controlsfx:8.20.7'
    compile group: 'org.snmp4j', name: 'snmp4j', version: '2.3.1'
    compile group: 'org.apache.commons', name: 'commons-email', version: '1.3.3'
    compile group: 'com.google.inject', name: 'guice', version: '3.0'
    compile group: 'tgm.hit.rtn', name: 'dLock', version: '+'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.google.code.gson:gson:2.3'
    testCompile 'org.apache.commons:commons-io:1.3.2'
    testCompile 'org.apache.sshd:sshd-core:0.12.0'
    testCompile 'org.controlsfx:controlsfx:8.20.7'
}

jar {
    manifest {
        attributes 'Main-Class': 'rtn.MainApp'
        
        def manifestCp = configurations.runtime.files.collect  {
            File file = it
            "lib/${file.name}"
        }.join(' ')

         configurations.runtime.getAllDependencies().withType(ProjectDependency).each {dep->

            def depProj = dep.getDependencyProject()
            def libFilePaths = project(depProj.path).libsDir.list().collect{ inFile-> "lib/${inFile}"  }.join(' ')
            logger.info"Adding libs from project ${depProj.name}: [- ${libFilePaths} -]"
            manifestCp += ' '+libFilePaths
        }

        logger.lifecycle("")
        logger.lifecycle("---Manifest-Class-Path: ${manifestCp}")
        attributes 'Class-Path': manifestCp
    }
}

project (':EMailExample') {
    apply plugin: 'java'
    apply plugin: 'application'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.apache.commons', name: 'commons-email', version: '1.3.3'
    }

    mainClassName = 'rtn.email.Main'
}

project (':TrapPrototype') {
    apply plugin: 'java'
    apply plugin: 'application'

    repositories {
        mavenCentral()

        maven {
            url "https://oosnmp.net/dist/release/" // snmp4j
        }
    }

    dependencies {
        compile 'com.jcraft:jsch:0.1.51'
        compile 'com.google.code.gson:gson:2.3'
        compile 'org.slf4j:slf4j-log4j12:1.7.7'
        compile group: 'org.snmp4j', name: 'snmp4j', version: '2.3.1'
        compile group: 'org.apache.commons', name: 'commons-email', version: '1.3.3'
        compile group: 'com.google.inject', name: 'guice', version: '3.0'

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'com.google.code.gson:gson:2.3'
        testCompile 'org.apache.commons:commons-io:1.3.2'
        testCompile 'org.apache.sshd:sshd-core:0.12.0'
    }

    mainClassName = 'rtn.prototype.snmp.trap.Main'
}

